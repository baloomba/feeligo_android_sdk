buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:0.12.+'
    }
}

allprojects {
    repositories {
        mavenCentral()
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.12'
}

apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

dependencies {
    // GRADLE DEPENDENCIES
    // SUPPORT
    compile project.ANDROID_SUPPORT_V4
    // SUPPORT
    compile project.ANDROID_SUPPORT_APPCOMPAT_V7
    // WSVOLLEY
    compile 'fr.baloomba:wsvolley:1.0.+'
    // VIEW PAGER INDICATOR
    compile 'fr.baloomba:viewpagerindicator:2.4.+'
}

android {
    compileSdkVersion Integer.parseInt(project.ANDROID_BUILD_SDK_VERSION)
    buildToolsVersion = project.ANDROID_BUILD_TOOLS_VERSION

    defaultConfig {
        minSdkVersion Integer.parseInt(project.ANDROID_BUILD_MIN_SDK_VERSION)
        targetSdkVersion Integer.parseInt(project.ANDROID_BUILD_TARGET_SDK_VERSION)
    }

    buildTypes {
        debug {
            buildConfigField "String", "FEELIGO_API_BASE_URL",
                    "\"http://stickersapi.feeligo.com/api\""
        }

        release {
            buildConfigField "String", "FEELIGO_API_BASE_URL",
                    "\"http://stickersapi.feeligo.com/api\""
        }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
}

group = 'fr.baloomba'
version = '1.0.1'

signing {
    required { has("release") && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    configuration = configurations.archives
    repositories.mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: sonatypeRepo) {
            authentication(userName: sonatypeUsername,
                    password: sonatypePassword)
        }

        pom.artifactId = 'feeligo'

        pom.project {
            name 'feeligo_android_sdk'
            packaging 'aar'
            description 'Android SDK for Feeligo'
            url 'https://github.com/baloomba/feeligo_android_sdk'

            scm {
                url 'scm:git@github.com:baloomba/feeligo_android_sdk.git'
                connection 'scm:git@github.com:baloomba/feeligo_android_sdk.git'
                developerConnection 'scm:git@github.com:baloomba/feeligo_android_sdk.git'
            }

            licenses {
                license {
                    name 'The MIT License (MIT)'
                    url 'http://www.opensource.org/licenses/mit-license.php'
                }
            }

            developers {
                developer {
                    id 'baloo'
                    name 'Clement BOULY'
                    email 'bouly.clement@gmail.com'
                }
            }
        }
    }
}

